/* This software is modified from
 * https://github.com/sparkfun/MMA8452_Accelerometer
 *  MMA8452Q Basic Example Code
 *  Nathan Seidle
 *  SparkFun Electronics
 *  November 5, 2012
 *
 * This code is public domain.
*/
/*
 *   Modified 18 March 2014 by yuuki.okamiya.yn@renesas.com
 *   Modified 29 April 2014 by masashi.fujimoto.eb@renesas.com
 */
#include <Arduino.h>
#include "mma8452q_fuji.h"
#include <wire.h>

void readAccelData(float *destination)
{
  byte rawData[6];  // x/y/z accel register data stored here
  readRegisters(OUT_X_MSB, 6, rawData);  // Read the six raw data registers into data array
  int temp_dest[3];
  // Loop to calculate 12-bit ADC and g value for each axis
  for(int i = 0; i < 3 ; i++)
  {
    int gCount = (rawData[i*2] << 8) | rawData[(i*2)+1];  //Combine the two 8 bit registers into one 12-bit number
    gCount >>= 4; //The registers are left align, here we right align the 12-bit integer

    // If the number is negative, we have to make it so manually (no 12-bit data type)
    if (rawData[i*2] > 0x7F)
    {
      gCount = ~gCount + 1;
      gCount *= -1;  // Transform into negative 2's complement #
    }

    temp_dest[i] = gCount; //Record this gCount into the 3 int array
  }

  // Now we'll calculate the accleration value into actual g's
  // Stores the real accel value in g's
  for (int i = 0 ; i < 3 ; i++)
  {
    destination[i] = (float) temp_dest[i] / ((1<<12)/(2*GSCALE));  // get actual g value, this depends on scale being set
  }
}

// Initialize the MMA8452 registers
// See the many application notes for more info on setting all of these registers:
// http://www.freescale.com/webapp/sps/site/prod_summary.jsp?code=MMA8452Q
bool initMMA8452()
{
  Wire.begin(); //Join the bus as a master

  byte c = readRegister(WHO_AM_I);  // Read WHO_AM_I register
  if (c != 0x2A) // WHO_AM_I should always be 0x2A
  {
      return 0;
  }

  MMA8452Standby();  // Must be in standby to change registers

  // Set up the full scale range to 2, 4, or 8g.
  byte fsr = GSCALE;
  if(fsr > 8) fsr = 8; //Easy error check
  fsr >>= 2; // Neat trick, see page 22. 00 = 2G, 01 = 4A, 10 = 8G
  writeRegister(XYZ_DATA_CFG, fsr);

  //The default data rate is 800Hz and we don't modify it in this example code

  MMA8452Active();  // Set to active to start reading
  return 1;
}

// Sets the MMA8452 to standby mode. It must be in standby to change most register settings
void MMA8452Standby()
{
  byte c = readRegister(CTRL_REG1);
  writeRegister(CTRL_REG1, c & ~(0x01)); //Clear the active bit to go into standby
}

// Sets the MMA8452 to active mode. Needs to be in this mode to output data
void MMA8452Active()
{
  byte c = readRegister(CTRL_REG1);
  writeRegister(CTRL_REG1, c | 0x01); //Set the active bit to begin detection
}

// Read bytesToRead sequentially, starting at addressToRead into the dest byte array
void readRegisters(byte addressToRead, int bytesToRead, byte * dest)
{
  Wire.beginTransmission(MMA8452_ADDRESS);
  Wire.write(addressToRead);
  Wire.endTransmission(false); //endTransmission but keep the connection active

  Wire.requestFrom(MMA8452_ADDRESS, bytesToRead); //Ask for bytes, once done, bus is released by default

  while(Wire.available() < bytesToRead); //Hang out until we get the # of bytes we expect

  for(int x = 0 ; x < bytesToRead ; x++)
    dest[x] = Wire.read();
}

// Read a single byte from addressToRead and return it as a byte
byte readRegister(byte addressToRead)
{
  Wire.beginTransmission(MMA8452_ADDRESS);
  Wire.write(addressToRead);
  Wire.endTransmission(false); //endTransmission but keep the connection active

  Wire.requestFrom(MMA8452_ADDRESS, 1); //Ask for 1 byte, once done, bus is released by default

  while(!Wire.available()) ; //Wait for the data to come back
  return Wire.read(); //Return this one byte
}

// Writes a single byte (dataToWrite) into addressToWrite
void writeRegister(byte addressToWrite, byte dataToWrite)
{
  Wire.beginTransmission(MMA8452_ADDRESS);
  Wire.write(addressToWrite);
  Wire.write(dataToWrite);
  Wire.endTransmission(); //Stop transmitting
}




void readAccelRawData(int *destination)
{
  byte rawData[6];  // x/y/z accel register data stored here
  readRegisters(OUT_X_MSB, 6, rawData);  // Read the six raw data registers into data array
  int temp_dest[3];
  // Loop to calculate 12-bit ADC and g value for each axis
  for(int i = 0; i < 3 ; i++)
  {
    int gCount = (rawData[i*2] << 8) | rawData[(i*2)+1];  //Combine the two 8 bit registers into one 12-bit number
    gCount >>= 4; //The registers are left align, here we right align the 12-bit integer

    // If the number is negative, we have to make it so manually (no 12-bit data type)
    if (rawData[i*2] > 0x7F)
    {
      gCount = ~gCount + 1;
      gCount *= -1;  // Transform into negative 2's complement #
    }

    temp_dest[i] = gCount; //Record this gCount into the 3 int array
  }

  // Now we'll calculate the accleration value into actual g's
  // Stores the real accel value in g's
  for (int i = 0 ; i < 3 ; i++)
  {
    destination[i] = temp_dest[i];  // get value
  }
}
void readAccelRawData(int *destination, int axis)
{
  byte rawData[2];  // x/y/z accel register data stored here
  readRegisters(OUT_X_MSB+2*axis, 2, rawData);  // Read the six raw data registers into data array
  int temp_dest;
  // Loop to calculate 12-bit ADC and g value for each axis
  int gCount = (rawData[0] << 8) | rawData[1];  //Combine the two 8 bit registers into one 12-bit number
  gCount >>= 4; //The registers are left align, here we right align the 12-bit integer

  // If the number is negative, we have to make it so manually (no 12-bit data type)
  if (rawData[0] > 0x7F)
  {
      gCount = ~gCount + 1;
      gCount *= -1;  // Transform into negative 2's complement #
  }

  temp_dest = gCount; //Record this gCount into the 3 int array

  // Now we'll calculate the accleration value into actual g's
  // Stores the real accel value in g's
  *destination = temp_dest;  // get value
}

